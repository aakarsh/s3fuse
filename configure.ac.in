AC_PREREQ([2.63])
AC_INIT([FUSE Driver for Amazon S3], [__VERSION__], [], [s3fuse])
AM_INIT_AUTOMAKE([no-define foreign])
AC_CONFIG_SRCDIR([src/logger.h])

AC_PROG_CXX
AC_PROG_CC

AC_ARG_ENABLE(
  [build-rpm], 
  [  --enable-build-rpm      Enable RPM build support],
  [case "${enableval}" in
    yes) build_rpm=true ;;
    no)  build_rpm=false ;;
    *) AC_MSG_ERROR([bad value ${enableval} for --enable-build-rpm]) ;;
   esac], [build_rpm=false])

AC_ARG_ENABLE(
  [darwin],
  [  --enable-darwin         Enable support for Darwin (Mac OS X)],
  [case "${enableval}" in
     yes) darwin=true ;;
     no)  darwin=false ;;
     *) AC_MSG_ERROR([bad value ${enableval} for --enable-darwin]) ;;
   esac], [darwin=false])

AM_CONDITIONAL([BUILD_RPM], [test x$build_rpm = xtrue])

if test "x$darwin" = xtrue; then
  LDFLAGS="$LDFLAGS -framework CoreFoundation -framework Security -L/usr/local/lib -L/opt/local/lib"
  CPPFLAGS="$CPPFLAGS -I/usr/local/include -I/opt/local/include"
  export PKG_CONFIG_PATH="$PKG_CONFIG_PATH:/opt/local/lib/pkgconfig:/usr/local/lib/pkgconfig"
else
  LDFLAGS="$LDFLAGS -Wl,--as-needed"
fi

AC_CHECK_LIB([boost_thread-mt], [main], [], [AC_CHECK_LIB([boost_thread], [main], [], [AC_MSG_FAILURE([cannot find boost thread library])])])
AC_CHECK_LIB([pthread], [pthread_create], [], [AC_MSG_ERROR([cannot find pthreads])])

PKG_CHECK_MODULES([DEPS_XML], [libxml-2.0 >= 2.7.6 libxml++-2.6 >= 2.28.0])
PKG_CHECK_MODULES([DEPS_CURL], [libcurl >= 7.0.0])
PKG_CHECK_MODULES([DEPS_FUSE], [fuse >= 2.7.3])

PKG_CHECK_EXISTS([libssl >= 0.9.8], [have_openssl=true], [have_openssl=false])
PKG_CHECK_EXISTS([gnutls >= 2.12.19], [have_gnutls=true], [have_gnutls=false])

if test "x$have_openssl"; then
  PKG_CHECK_MODULES([DEPS_OPENSSL], [libssl >= 0.9.8])
  CPPFLAGS="$CPPFLAGS -DHAVE_OPENSSL"
fi

if test "x$have_gnutls"; then
  PKG_CHECK_MODULES([DEPS_GNUTLS], [gnutls >= 2.12.19])
  CPPFLAGS="$CPPFLAGS -DHAVE_GNUTLS"
fi

AC_CHECK_HEADERS([inttypes.h stdint.h stdlib.h string.h sys/time.h syslog.h])

AC_HEADER_STDBOOL
AC_TYPE_UID_T
AC_C_INLINE
AC_TYPE_MODE_T
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_TYPE_SSIZE_T
AC_STRUCT_ST_BLOCKS
AC_TYPE_UINT64_T
AC_TYPE_UINT8_T

AC_FUNC_MALLOC
AC_CHECK_FUNCS([ftruncate gettimeofday memset strchr strerror strstr strtol])

AC_CONFIG_FILES([Makefile src/Makefile])
AM_COND_IF([BUILD_RPM], [AC_CONFIG_FILES([dist/Makefile])])

AC_OUTPUT
